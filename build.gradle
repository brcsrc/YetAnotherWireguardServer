
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.6-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'de.undercouch.download' version '4.1.1'
	id 'jacoco'
}
jacoco {
	toolVersion = '0.8.9'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.brcsrc'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

dependencies {
	// http and api documentation
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.session:spring-session-core'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

	// security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// persistence
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'javax.persistence:javax.persistence-api:2.2'
	implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
	implementation 'org.hibernate.orm:hibernate-community-dialects:6.3.1.Final'
    implementation 'org.testng:testng:7.1.0'

	// QR code generation
	implementation 'com.google.zxing:core:3.5.3'
	implementation 'com.google.zxing:javase:3.5.3'

    // testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

// create local dir 'lib' and run ./gradlew copyDependenciesToLocalRepo
task copyDependenciesToLocalRepo(type: Copy) {
	from configurations.runtimeClasspath
	into 'lib'
}

// caches gradle to speed up docker container build
task downloadGradleBin(type: Download) {
	src 'https://services.gradle.org/distributions/gradle-8.9-bin.zip'  // URL of the Gradle distribution
	dest new File(project.rootDir, 'gradle/wrapper/gradle-8.9-bin.zip')
	overwrite false
}

repositories {
	flatDir {
		dirs 'lib'
	}
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

// make test runs log output only on test failures
tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport

	testLogging {
		events "passed", "failed"  					// Only log "passed" and "failed" events
		exceptionFormat = "full"  					// Show full stack trace for failures
	}
	beforeTest {
		testLogging.showStandardStreams = true  	// show standard output from logger
	}
	afterTest { descriptor, result ->
		switch (result.resultType) {
			case (TestResult.ResultType.FAILURE):
				println("❌ Test failed: ${descriptor.name}")
				break;
			case (TestResult.ResultType.SUCCESS):
				println("✅ Test passed: ${descriptor.name}")
				break;
			case (TestResult.ResultType.SKIPPED):
				println("⏩ Test skipped: ${descriptor.name}")
				break;
		}
	}
}

task checkJarExists {
	doLast {
		def jarFile = file('build/libs/yaws-0.0.1-SNAPSHOT.jar')
		if (!jarFile.exists()) {
			configurations.runtimeClasspath.resolve()
		}
	}
}
test.dependsOn checkJarExists

